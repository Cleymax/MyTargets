/*
 * Copyright (C) 2018 Florian Dreier
 *
 * This file is part of MyTargets.
 *
 * MyTargets is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * MyTargets is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.ajoberstar.grgit:grgit-gradle:4.0.2"
    }
}

apply plugin: "org.ajoberstar.grgit"

ext {
    try {
        git = Grgit.open()
        gitVersionCode = git.tag.list().size()
        gitVersionName = git.describe()
                .replace("v", "")
                .replace("_b", "-Beta")
                .replace("_rc", "-RC")

        gitBranchName = System.getenv("TRAVIS") == "true" ? System.getenv('TRAVIS_BRANCH') : git.branch.current().name
    } catch (Exception ignored) {
        gitVersionCode = 1
        gitVersionName = "debug"
        gitBranchName = "debug"
    }
}
